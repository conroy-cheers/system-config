# Edit this configuration file to define what should be installed on
# your system.  Help is available in the configuration.nix(5) man page
# and in the NixOS manual (accessible by running ‘nixos-help’).

{
  inputs,
  lib,
  pkgs,
  config,
  ...
}:

let
  # Session package for home-manager managed .wayland-session
  hm-wayland-desktop = pkgs.stdenvNoCC.mkDerivation {
    name = "hm-wayland-desktop";
    src = pkgs.makeDesktopItem {
      name = "home-manager-wayland";
      destination = "/share/wayland-sessions";
      desktopName = "home-manager (Wayland)";
      exec = "/home/conroy/.wayland-session";
      type = "Application";
    };
    installPhase = ''
      mkdir -p $out
      cp -a $src/* $out
    '';
    passthru.providedSessions = [ "home-manager-wayland" ];
  };
in
{
  imports = [
    inputs.hardware.nixosModules.common-cpu-amd
    inputs.hardware.nixosModules.common-pc-ssd
    inputs.hardware.nixosModules.common-gpu-nvidia-nonprime
    ./hardware-configuration.nix
    ./disko.nix
    inputs.impermanence.nixosModules.impermanence
    ./impermanence.nix
    ./network.nix
    ./fan2go.nix
  ];

  ### Set boot options
  boot = {
    # Use the systemd-boot boot loader.
    loader = {
      efi.canTouchEfiVariables = true;
      grub.enable = false;

      limine = {
        enable = true;
        extraEntries = ''
          /Windows
            protocol: efi
            path: uuid(89a1eedd-3c95-47bf-80c6-bd131131ee54):/EFI/Microsoft/Boot/bootmgfw.efi
        '';
      };
    };

    # Enable running aarch64 binaries using qemu
    binfmt = {
      emulatedSystems = [
        "aarch64-linux"
        "wasm32-wasi"
        "x86_64-windows"
      ];
    };

    supportedFilesystems = [ "btrfs" ];
  };

  networking.hostName = "brick"; # Define your hostname.
  ### Pick only one of the below networking options.
  # networking.wireless.enable = true;  # Enables wireless support via wpa_supplicant.
  # networking.networkmanager.enable = true;  # Easiest to use and most distros use this by default.

  ### Set your time zone.
  time.timeZone = "Australia/Melbourne";
  # Dual boot with Windows
  time.hardwareClockInLocalTime = true;

  corncheese = {
    development = {
      enable = true;
      remoteBuilders.enable = true;
    };
    theming = {
      enable = true;
      theme = "catppuccin";
    };
    wm = {
      enable = true;
      audio = {
        enable = true;
        equalizer.enable = true;
      };
      nvidia = true;
      gaming.enable = true;
    };
  };

  # log conroy into atuin sync
  age.secrets."corncheese.atuin.key" = {
    rekeyFile = lib.repoSecret "corncheese/atuin/key.age";
    owner = "conroy";
    mode = "0400";
  };
  home-manager.users.conroy = {
    corncheese = {
      shell.atuin = {
        key = config.age.secrets."corncheese.atuin.key".path;
      };
    };
  };

  andromeda = {
    development = {
      enable = true;
      tailscale.enable = false;
      remoteBuilders.enable = true;
      nixDaemonSecrets.enable = true;
    };
  };

  ### Configure network proxy if necessary
  # networking.proxy.default = "http://user:password@proxy:port/";
  # networking.proxy.noProxy = "127.0.0.1,localhost,internal.domain";

  ### Select internationalisation properties.
  # i18n.defaultLocale = "en_US.UTF-8";
  # console = {
  #   font = "Lat2-Terminus16";
  #   keyMap = "us";
  #   useXkbConfig = true; # use xkbOptions in tty.
  # };

  nix = {
    settings = {
      trusted-users = [ "conroy" ];
    };
  };

  # nopasswd for sudo
  security.sudo-rs = {
    enable = true; # !config.security.sudo.enable;
    inherit (config.security.sudo) extraRules;
  };
  security.sudo = {
    enable = false;
    extraRules = [
      {
        users = [ "conroy" ];
        commands = [
          {
            command = "ALL";
            options = [ "NOPASSWD" ]; # "SETENV" # Adding the following could be a good idea
          }
        ];
      }
    ];
  };

  ### Fonts
  fonts.fontconfig.enable = true;

  hardware = {
    graphics.enable = true;
    nvidia.powerManagement = {
      enable = true;
    };
  };

  services.hardware = {
    openrgb.enable = true;
  };

  ### Wayland specific
  services.xserver = {
    enable = false; # disable xserver
    videoDrivers = lib.mkDefault [ "nvidia" ];
  };

  services.displayManager.sessionPackages = lib.mkBefore [
    hm-wayland-desktop
  ];
  services.greetd = {
    enable = true;
    # settings = {
    #   default_session = {
    #     user = "conroy";
    #     # .wayland-session is a script generated by home-manager, which links to the current wayland compositor(sway/hyprland or others).
    #     # with such a vendor-no-locking script, we can switch to another wayland compositor without modifying greetd's config here.
    #     command = "$HOME/.wayland-session"; # start a wayland session directly without a login manager
    #     # command = "${pkgs.greetd.tuigreet}/bin/tuigreet --time --cmd $HOME/.wayland-session";  # start wayland session with a TUI login manager
    #   };
    # };
    settings = {
      default_session = {
        command = ''
          ${lib.getExe pkgs.greetd.tuigreet} --sessions \
            ${config.services.displayManager.sessionData.desktops}/share/xsessions:${config.services.displayManager.sessionData.desktops}/share/wayland-sessions \
            --remember --remember-user-session
        '';
        user = "conroy";
      };
    };
  };

  ### Enable the OpenSSH daemon.
  services.openssh = {
    enable = true;
    settings = {
      # Forbid root login through SSH.
      PermitRootLogin = "no";
      # Use keys only. Remove if you want to SSH using password (not recommended)
      PasswordAuthentication = false;
    };
  };

  ### Enable CUPS to print documents.
  services.printing.enable = true;

  ### Enable sound.
  services.dbus = {
    enable = true;
    packages = [ pkgs.dconf ];
  };

  ### udev packages
  services.udev.packages = with pkgs; [
    picoprobe-udev-rules
  ];

  ### Enable touchpad support (enabled default in most desktopManager).
  # services.xserver.libinput.enable = true;

  # Virtualisation
  virtualisation.docker.enable = true;

  age.secrets."conroy.user.password" = {
    rekeyFile = lib.repoSecret "home/conroy/user/password.age";
    mode = "440";
  };

  ### Define a user account. Don't forget to set a password with `passwd`.
  users.users.conroy = {
    isNormalUser = true;
    hashedPasswordFile = config.age.secrets."conroy.user.password".path;
    openssh.authorizedKeys.keys = [
      "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAINKbNTRUenigTtrUSGKImYezWzT/KFOR7dZSpSuvsKNY" # conroy-home
    ];
    shell = pkgs.fish;
    extraGroups = [
      "wheel"
      "docker"
      "plugdev"
      "dialout"
    ];
  };

  programs.fish.enable = true;

  # udisks2 for mounting USB disks
  services.udisks2.enable = true;

  # thunar file manager
  programs.thunar.enable = true;

  ### Enable plymouth (bootscreen customizations)
  boot.plymouth = {
    enable = true;
    # theme = lib.mkForce "breeze";
  };
  boot.initrd.systemd.enable = true;

  # List packages installed in system profile. To search, run:
  # $ nix search wget
  environment.systemPackages = with pkgs; [
    neovim
    wget
    udiskie
  ];

  programs.git = {
    enable = true;
    package = pkgs.gitFull;
    config.credential.helper = "libsecret";
  };

  # Open ports in the firewall.
  # networking.firewall.allowedTCPPorts = [ ... ];
  # networking.firewall.allowedUDPPorts = [ ... ];
  # Or disable the firewall altogether.
  networking.firewall.enable = false;

  # This value determines the NixOS release from which the default
  # settings for stateful data, like file locations and database versions
  # on your system were taken. It‘s perfectly fine and recommended to leave
  # this value at the release version of the first install of this system.
  # Before changing this value read the documentation for this option
  # (e.g. man configuration.nix or on https://nixos.org/nixos/options.html).
  system.stateVersion = "24.05"; # Did you read the comment?
}
